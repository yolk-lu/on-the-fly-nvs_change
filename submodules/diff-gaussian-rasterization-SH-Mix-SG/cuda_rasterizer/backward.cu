/*
 * Copyright (C) 2023 - 2025 - 2025, Inria
 * GRAPHDECO research group, https://team.inria.fr/graphdeco
 * All rights reserved.
 *
 * This software is free for non-commercial, research and evaluation use 
 * under the terms of the LICENSE.md file.
 *
 * For inquiries contact  george.drettakis@inria.fr
 */

#include "backward.h"
#include "auxiliary.h"
#include <cooperative_groups.h>
#include <cooperative_groups/reduce.h>
namespace cg = cooperative_groups;

__device__ __forceinline__ float sq(float x) { return x * x; }


// Backward pass for conversion of spherical harmonics to RGB for
// each Gaussian.
__device__ void computeColorFromSH(int idx, int deg, int max_coeffs, const glm::vec3* means, glm::vec3 campos, const float* dc, const float* shs, const bool* clamped, const glm::vec3* dL_dcolor, glm::vec3* dL_dmeans, glm::vec3* dL_ddc, glm::vec3* dL_dshs)
{
	// Compute intermediate values, as it is done during forward
	glm::vec3 pos = means[idx];
	glm::vec3 dir_orig = pos - campos;
	glm::vec3 dir = dir_orig / glm::length(dir_orig);

	glm::vec3* direct_color = ((glm::vec3*)dc) + idx;
	glm::vec3* sh = ((glm::vec3*)shs) + idx * max_coeffs;

	// Use PyTorch rule for clamping: if clamping was applied,
	// gradient becomes 0.
	glm::vec3 dL_dRGB = dL_dcolor[idx];
	dL_dRGB.x *= clamped[3 * idx + 0] ? 0 : 1;
	dL_dRGB.y *= clamped[3 * idx + 1] ? 0 : 1;
	dL_dRGB.z *= clamped[3 * idx + 2] ? 0 : 1;

	glm::vec3 dRGBdx(0, 0, 0);
	glm::vec3 dRGBdy(0, 0, 0);
	glm::vec3 dRGBdz(0, 0, 0);
	float x = dir.x;
	float y = dir.y;
	float z = dir.z;

	// Target location for this Gaussian to write SH gradients to
	glm::vec3* dL_ddirect_color = dL_ddc + idx;
	glm::vec3* dL_dsh = dL_dshs + idx * max_coeffs;

	// No tricks here, just high school-level calculus.
	float dRGBdsh0 = SH_C0;
	dL_ddirect_color[0] = dRGBdsh0 * dL_dRGB;
	if (deg > 0)
	{
		float dRGBdsh1 = -SH_C1 * SH_W1 * y;
		float dRGBdsh2 = SH_C1 * SH_W1 * z;
		float dRGBdsh3 = -SH_C1 * SH_W1 * x;
		dL_dsh[0] = dRGBdsh1 * dL_dRGB;
		dL_dsh[1] = dRGBdsh2 * dL_dRGB;
		dL_dsh[2] = dRGBdsh3 * dL_dRGB;

		dRGBdx = -SH_C1 * SH_W1 * sh[2];
		dRGBdy = -SH_C1 * SH_W1 * sh[0];
		dRGBdz = SH_C1 * SH_W1 * sh[1];

		if (deg > 1)
		{
			float xx = x * x, yy = y * y, zz = z * z;
			float xy = x * y, yz = y * z, xz = x * z;

			float dRGBdsh4 = SH_C2[0] * SH_W2 * xy;
			float dRGBdsh5 = SH_C2[1] * SH_W2 * yz;
			float dRGBdsh6 = SH_C2[2] * SH_W2 * (2.f * zz - xx - yy);
			float dRGBdsh7 = SH_C2[3] * SH_W2 * xz;
			float dRGBdsh8 = SH_C2[4] * SH_W2 * (xx - yy);
			dL_dsh[3] = dRGBdsh4 * dL_dRGB;
			dL_dsh[4] = dRGBdsh5 * dL_dRGB;
			dL_dsh[5] = dRGBdsh6 * dL_dRGB;
			dL_dsh[6] = dRGBdsh7 * dL_dRGB;
			dL_dsh[7] = dRGBdsh8 * dL_dRGB;

			dRGBdx += SH_C2[0] * SH_W2 * y * sh[3] + SH_C2[2] * SH_W2 * 2.f * -x * sh[5] + SH_C2[3] * SH_W2 * z * sh[6] + SH_C2[4] * SH_W2 * 2.f * x * sh[7];
			dRGBdy += SH_C2[0] * SH_W2 * x * sh[3] + SH_C2[1] * SH_W2 * z * sh[4] + SH_C2[2] * SH_W2 * 2.f * -y * sh[5] + SH_C2[4] * SH_W2 * 2.f * -y * sh[7];
			dRGBdz += SH_C2[1] * SH_W2 * y * sh[4] + SH_C2[2] * SH_W2 * 2.f * 2.f * z * sh[5] + SH_C2[3] * SH_W2 * x * sh[6];

			if (deg > 2)
			{
				float dRGBdsh9 = SH_C3[0] * SH_W3 * y * (3.f * xx - yy);
				float dRGBdsh10 = SH_C3[1] * SH_W3 * xy * z;
				float dRGBdsh11 = SH_C3[2] * SH_W3 * y * (4.f * zz - xx - yy);
				float dRGBdsh12 = SH_C3[3] * SH_W3 * z * (2.f * zz - 3.f * xx - 3.f * yy);
				float dRGBdsh13 = SH_C3[4] * SH_W3 * x * (4.f * zz - xx - yy);
				float dRGBdsh14 = SH_C3[5] * SH_W3 * z * (xx - yy);
				float dRGBdsh15 = SH_C3[6] * SH_W3 * x * (xx - 3.f * yy);
				dL_dsh[8] = dRGBdsh9 * dL_dRGB;
				dL_dsh[9] = dRGBdsh10 * dL_dRGB;
				dL_dsh[10] = dRGBdsh11 * dL_dRGB;
				dL_dsh[11] = dRGBdsh12 * dL_dRGB;
				dL_dsh[12] = dRGBdsh13 * dL_dRGB;
				dL_dsh[13] = dRGBdsh14 * dL_dRGB;
				dL_dsh[14] = dRGBdsh15 * dL_dRGB;

				dRGBdx += (
					SH_C3[0] * SH_W3 * sh[8] * 3.f * 2.f * xy +
					SH_C3[1] * SH_W3 * sh[9] * yz +
					SH_C3[2] * SH_W3 * sh[10] * -2.f * xy +
					SH_C3[3] * SH_W3 * sh[11] * -3.f * 2.f * xz +
					SH_C3[4] * SH_W3 * sh[12] * (-3.f * xx + 4.f * zz - yy) +
					SH_C3[5] * SH_W3 * sh[13] * 2.f * xz +
					SH_C3[6] * SH_W3 * sh[14] * 3.f * (xx - yy));

				dRGBdy += (
					SH_C3[0] * SH_W3 * sh[8] * 3.f * (xx - yy) +
					SH_C3[1] * SH_W3 * sh[9] * xz +
					SH_C3[2] * SH_W3 * sh[10] * (-3.f * yy + 4.f * zz - xx) +
					SH_C3[3] * SH_W3 * sh[11] * -3.f * 2.f * yz +
					SH_C3[4] * SH_W3 * sh[12] * -2.f * xy +
					SH_C3[5] * SH_W3 * sh[13] * -2.f * yz +
					SH_C3[6] * SH_W3 * sh[14] * -3.f * 2.f * xy);

				dRGBdz += (
					SH_C3[1] * SH_W3 * sh[9] * xy +
					SH_C3[2] * SH_W3 * sh[10] * 4.f * 2.f * yz +
					SH_C3[3] * SH_W3 * sh[11] * 3.f * (2.f * zz - xx - yy) +
					SH_C3[4] * SH_W3 * sh[12] * 4.f * 2.f * xz +
					SH_C3[5] * SH_W3 * sh[13] * (xx - yy));
			}
		}
	}

	// The view direction is an input to the computation. View direction
	// is influenced by the Gaussian's mean, so SHs gradients
	// must propagate back into 3D position.
	glm::vec3 dL_ddir(glm::dot(dRGBdx, dL_dRGB), glm::dot(dRGBdy, dL_dRGB), glm::dot(dRGBdz, dL_dRGB));

	// Account for normalization of direction
	float3 dL_dmean = dnormvdv(float3{ dir_orig.x, dir_orig.y, dir_orig.z }, float3{ dL_ddir.x, dL_ddir.y, dL_ddir.z });

	// Gradients of loss w.r.t. Gaussian means, but only the portion 
	// that is caused because the mean affects the view-dependent color.
	// Additional mean gradient is accumulated in below methods.
	dL_dmeans[idx] += glm::vec3(dL_dmean.x, dL_dmean.y, dL_dmean.z);
}

// Backward pass for Spherical Gaussians (SG)
__device__ void computeSGIGrad(int idx, int max_coeffs_sg, const float* shs, const float* dL_dsh_coeffs, float* dL_dshs_sg)
{
	// Params: SG parameters (Forward)
	const float* params = shs + idx * max_coeffs_sg;
	
	// dL_dparams: SG parameter gradients (Backward Output)
	// Cast result buffer to float* to match signature (Wait, signature above takes glm::vec3* usually? No, let's fix signature to float*)
	// But `preprocessCUDA` calls it. `dL_dsh` in preprocess is `float*`. 
	// However, `computeSGIGrad` definition in this file previously had `glm::vec3*`.
	// We should change it to `float*` for flexibility with stride 7.
	float* dL_dparams = dL_dshs_sg + idx * max_coeffs_sg;
	
	int num_lobes = max_coeffs_sg / 7;

	// Temp storage for Y_lm(axis) and Grad Y_lm(axis)
	float basis[16];
	glm::vec3 components_grad[16];

	// Constants for Normalization: sqrt(4pi / (2l+1))
	const float C_norm[4] = {
		3.544907701811032f, 2.046653415892977f, 1.585330919042404f, 1.354055224345298f
	};

	// Start indices for each band
	const int band_start[] = {0, 1, 4, 9};
	const int band_end[] = {0, 3, 8, 15};

	for (int k = 0; k < num_lobes; ++k)
	{
		const float* lobe_params = params + k * 7;
		float* lobe_grads = dL_dparams + k * 7;

		glm::vec3 amplitude = {lobe_params[0], lobe_params[1], lobe_params[2]};
		glm::vec3 axis = {lobe_params[3], lobe_params[4], lobe_params[5]};
		float sharpness = lobe_params[6];

		float axis_len = glm::length(axis);
		glm::vec3 axis_norm = axis;
		if (axis_len > 1e-6f)
			axis_norm = axis / axis_len;
		
		evalSHBasis(axis_norm, basis);
		dEvalSHBasis(axis_norm, components_grad);
		
		float s = sharpness; // stored as log lambda
		float lambda = exp(s); // lambda

		glm::vec3 dL_dA = {0.f, 0.f, 0.f};
		glm::vec3 dL_dAxisNorm = {0.f, 0.f, 0.f};
		float dL_dLambda = 0.f;

		const glm::vec3* dL_dC = (const glm::vec3*)dL_dsh_coeffs;

		// Iterate over bands 0 to 3
		for (int l = 0; l <= 3; ++l) {
			float g = evalZHApproxVal(l, lambda);
			float dg = evalZHApproxGrad(l, lambda);
			float scale = C_norm[l];
			
			for (int i = band_start[l]; i <= band_end[l]; ++i) {
				// f_lm = A * scale * g * Y
				// dL/dA += dL/df * scale * g * Y
				// dL/dLambda += dot(dL/df, A) * scale * dg * Y
				// dL/dAxis += dot(dL/df, A) * scale * g * dY/dAxis
				
				glm::vec3 grad_f = dL_dC[i]; 
				float Y = basis[i];
				glm::vec3 dY = components_grad[i];

				dL_dA += grad_f * (scale * g * Y);
				dL_dLambda += glm::dot(grad_f, amplitude) * (scale * dg * Y);
				dL_dAxisNorm += glm::dot(grad_f, amplitude) * (scale * g) * dY;
			}
		}

		// Propagate to parameters (Respect stride 7)
		lobe_grads[0] = dL_dA.x;
		lobe_grads[1] = dL_dA.y;
		lobe_grads[2] = dL_dA.z;
		
		// Axis gradient (backprop normalization)
		glm::vec3 dL_dAxis = {0.f, 0.f, 0.f};
		if (axis_len > 1e-6f) {
			glm::vec3 term = dL_dAxisNorm / axis_len;
			dL_dAxis = term - axis_norm * glm::dot(axis_norm, term);
		}
		lobe_grads[3] = dL_dAxis.x;
		lobe_grads[4] = dL_dAxis.y;
		lobe_grads[5] = dL_dAxis.z;

		// Sharpness s gradient: dL/ds = dL/dlambda * dlambda/ds = dL/dlambda * lambda
		float dL_ds = dL_dLambda * lambda;
		lobe_grads[6] = dL_ds;
	}
}


// Backward version of INVERSE 2D covariance matrix computation
// (due to length launched as separate kernel before other 
// backward steps contained in preprocess)
__global__ void computeCov2DCUDA(int P,
	const float3* means,
	const int* radii,
	const float* cov3Ds,
	const float h_x, float h_y,
	const float tan_fovx, float tan_fovy,
	const float* viewmatrix,
	const float* opacities,
	const float* dL_dconics,
	float* dL_dopacity,
	const float* dL_dinvdepth,
	float3* dL_dmeans_loc,
	float* dL_dcov,
	glm::mat3* dL_dRs)
{
	auto idx = cg::this_grid().thread_rank();
	if (idx >= P || !(radii[idx] > 0))
		return;

	// Reading location of 3D covariance for this Gaussian
	const float* cov3D = cov3Ds + 6 * idx;

	// Fetch gradients, recompute 2D covariance and relevant 
	// intermediate forward results needed in the backward.
	float3 mean = means[idx];
	float3 dL_dconic = { dL_dconics[4 * idx], dL_dconics[4 * idx + 1], dL_dconics[4 * idx + 3] };
	float3 t = transformPoint4x3(mean, viewmatrix); // rot
	
	const float limx = 1.3f * tan_fovx;
	const float limy = 1.3f * tan_fovy;
	const float txtz = t.x / t.z;
	const float tytz = t.y / t.z;
	t.x = min(limx, max(-limx, txtz)) * t.z;
	t.y = min(limy, max(-limy, tytz)) * t.z;
	
	const float x_grad_mul = txtz < -limx || txtz > limx ? 0 : 1;
	const float y_grad_mul = tytz < -limy || tytz > limy ? 0 : 1;

	glm::mat3 J = glm::mat3(h_x / t.z, 0.0f, -(h_x * t.x) / (t.z * t.z),
		0.0f, h_y / t.z, -(h_y * t.y) / (t.z * t.z),
		0, 0, 0);

	glm::mat3 W = glm::mat3(
		viewmatrix[0], viewmatrix[4], viewmatrix[8],
		viewmatrix[1], viewmatrix[5], viewmatrix[9],
		viewmatrix[2], viewmatrix[6], viewmatrix[10]);

	glm::mat3 Vrk = glm::mat3(
		cov3D[0], cov3D[1], cov3D[2],
		cov3D[1], cov3D[3], cov3D[4],
		cov3D[2], cov3D[4], cov3D[5]);

	glm::mat3 T = W * J;

	glm::mat3 cov2D = glm::transpose(T) * glm::transpose(Vrk) * T;

	// Use helper variables for 2D covariance entries. More compact.
	float c_xx = cov2D[0][0];
	float c_xy = cov2D[0][1];
	float c_yy = cov2D[1][1];
	
	constexpr float h_var = 0.3f;
#ifdef DGR_FIX_AA
	 const float det_cov = c_xx * c_yy - c_xy * c_xy;
	c_xx += h_var;
	c_yy += h_var;
	const float det_cov_plus_h_cov = c_xx * c_yy - c_xy * c_xy;
	const float h_convolution_scaling = sqrt(max(0.000025f, det_cov / det_cov_plus_h_cov)); // max for numerical stability
	const float dL_dopacity_v = dL_dopacity[idx];
	const float d_h_convolution_scaling = dL_dopacity_v * opacities[idx];
	dL_dopacity[idx] = dL_dopacity_v * h_convolution_scaling;
	const float d_inside_root = (det_cov / det_cov_plus_h_cov) <= 0.000025f ? 0.f : d_h_convolution_scaling / (2 * h_convolution_scaling);
#else
	c_xx += h_var;
	c_yy += h_var;
#endif
	
	float dL_dc_xx = 0;
	float dL_dc_xy = 0;
	float dL_dc_yy = 0;
#ifdef DGR_FIX_AA
	{
	               // https://www.wolframalpha.com/input?i=d+%28%28x*y+-+z%5E2%29%2F%28%28x%2Bw%29*%28y%2Bw%29+-+z%5E2%29%29+%2Fdx
	               // https://www.wolframalpha.com/input?i=d+%28%28x*y+-+z%5E2%29%2F%28%28x%2Bw%29*%28y%2Bw%29+-+z%5E2%29%29+%2Fdz
	const float x = c_xx;
	const float y = c_yy;
	const float z = c_xy;
	const float w = h_var;
	const float denom_f = d_inside_root / sq(w * w + w * (x + y) + x * y - z * z);
	const float dL_dx = w * (w * y + y * y + z * z) * denom_f;
	const float dL_dy = w * (w * x + x * x + z * z) * denom_f;
	const float dL_dz = -2.f * w * z * (w + x + y) * denom_f;
	dL_dc_xx = dL_dx;
	dL_dc_yy = dL_dy;
	dL_dc_xy = dL_dz;
	}
#endif
	
	float denom = c_xx * c_yy - c_xy * c_xy;
	float denom2inv = 1.0f / ((denom * denom) + 0.0000001f);

	if (denom2inv != 0)
	{
		// Gradients of loss w.r.t. entries of 2D covariance matrix,
		// given gradients of loss w.r.t. conic matrix (inverse covariance matrix).
		// e.g., dL / da = dL / d_conic_a * d_conic_a / d_a
		
		dL_dc_xx += denom2inv * (-c_yy * c_yy * dL_dconic.x + 2 * c_xy * c_yy * dL_dconic.y + (denom - c_xx * c_yy) * dL_dconic.z);
		dL_dc_yy += denom2inv * (-c_xx * c_xx * dL_dconic.z + 2 * c_xx * c_xy * dL_dconic.y + (denom - c_xx * c_yy) * dL_dconic.x);
		dL_dc_xy += denom2inv * 2 * (c_xy * c_yy * dL_dconic.x - (denom + 2 * c_xy * c_xy) * dL_dconic.y + c_xx * c_xy * dL_dconic.z);
		
		// Gradients of loss L w.r.t. each 3D covariance matrix (Vrk) entry,
		// given gradients w.r.t. 2D covariance matrix (diagonal).
		// cov2D = transpose(T) * transpose(Vrk) * T;
		dL_dcov[6 * idx + 0] = (T[0][0] * T[0][0] * dL_dc_xx + T[0][0] * T[1][0] * dL_dc_xy + T[1][0] * T[1][0] * dL_dc_yy);
		dL_dcov[6 * idx + 3] = (T[0][1] * T[0][1] * dL_dc_xx + T[0][1] * T[1][1] * dL_dc_xy + T[1][1] * T[1][1] * dL_dc_yy);
		dL_dcov[6 * idx + 5] = (T[0][2] * T[0][2] * dL_dc_xx + T[0][2] * T[1][2] * dL_dc_xy + T[1][2] * T[1][2] * dL_dc_yy);
		
		// Gradients of loss L w.r.t. each 3D covariance matrix (Vrk) entry,
		// given gradients w.r.t. 2D covariance matrix (off-diagonal).
		// Off-diagonal elements appear twice --> double the gradient.
		// cov2D = transpose(T) * transpose(Vrk) * T;
		dL_dcov[6 * idx + 1] = 2 * T[0][0] * T[0][1] * dL_dc_xx + (T[0][0] * T[1][1] + T[0][1] * T[1][0]) * dL_dc_xy + 2 * T[1][0] * T[1][1] * dL_dc_yy;
		dL_dcov[6 * idx + 2] = 2 * T[0][0] * T[0][2] * dL_dc_xx + (T[0][0] * T[1][2] + T[0][2] * T[1][0]) * dL_dc_xy + 2 * T[1][0] * T[1][2] * dL_dc_yy;
		dL_dcov[6 * idx + 4] = 2 * T[0][2] * T[0][1] * dL_dc_xx + (T[0][1] * T[1][2] + T[0][2] * T[1][1]) * dL_dc_xy + 2 * T[1][1] * T[1][2] * dL_dc_yy;
	}
	else
	{
		for (int i = 0; i < 6; i++)
			dL_dcov[6 * idx + i] = 0;
	}

	// Gradients of loss w.r.t. upper 2x3 portion of intermediate matrix T
	// cov2D = transpose(T) * transpose(Vrk) * T;
	float dL_dT00 = 2 * (T[0][0] * Vrk[0][0] + T[0][1] * Vrk[0][1] + T[0][2] * Vrk[0][2]) * dL_dc_xx +
	(T[1][0] * Vrk[0][0] + T[1][1] * Vrk[0][1] + T[1][2] * Vrk[0][2]) * dL_dc_xy;
	float dL_dT01 = 2 * (T[0][0] * Vrk[1][0] + T[0][1] * Vrk[1][1] + T[0][2] * Vrk[1][2]) * dL_dc_xx +
	(T[1][0] * Vrk[1][0] + T[1][1] * Vrk[1][1] + T[1][2] * Vrk[1][2]) * dL_dc_xy;
	float dL_dT02 = 2 * (T[0][0] * Vrk[2][0] + T[0][1] * Vrk[2][1] + T[0][2] * Vrk[2][2]) * dL_dc_xx +
	(T[1][0] * Vrk[2][0] + T[1][1] * Vrk[2][1] + T[1][2] * Vrk[2][2]) * dL_dc_xy;
	float dL_dT10 = 2 * (T[1][0] * Vrk[0][0] + T[1][1] * Vrk[0][1] + T[1][2] * Vrk[0][2]) * dL_dc_yy +
	(T[0][0] * Vrk[0][0] + T[0][1] * Vrk[0][1] + T[0][2] * Vrk[0][2]) * dL_dc_xy;
	float dL_dT11 = 2 * (T[1][0] * Vrk[1][0] + T[1][1] * Vrk[1][1] + T[1][2] * Vrk[1][2]) * dL_dc_yy +
	(T[0][0] * Vrk[1][0] + T[0][1] * Vrk[1][1] + T[0][2] * Vrk[1][2]) * dL_dc_xy;
	float dL_dT12 = 2 * (T[1][0] * Vrk[2][0] + T[1][1] * Vrk[2][1] + T[1][2] * Vrk[2][2]) * dL_dc_yy +
	(T[0][0] * Vrk[2][0] + T[0][1] * Vrk[2][1] + T[0][2] * Vrk[2][2]) * dL_dc_xy;

	// Gradients of loss w.r.t. upper 3x2 non-zero entries of Jacobian matrix
	// T = W * J
	float dL_dJ00 = W[0][0] * dL_dT00 + W[0][1] * dL_dT01 + W[0][2] * dL_dT02;
	float dL_dJ02 = W[2][0] * dL_dT00 + W[2][1] * dL_dT01 + W[2][2] * dL_dT02;
	float dL_dJ11 = W[1][0] * dL_dT10 + W[1][1] * dL_dT11 + W[1][2] * dL_dT12;
	float dL_dJ12 = W[2][0] * dL_dT10 + W[2][1] * dL_dT11 + W[2][2] * dL_dT12;

	// // Gradients of loss w.r.t. W
	// glm::mat3 dL_dW = glm::mat3(
	// 	dL_dT00 * J[0][0] + dL_dT02 * J[0][2], dL_dT01 * J[1][1] + dL_dT02 * J[1][2], 0.0f, 
	// 	dL_dT10 * J[0][0] + dL_dT12 * J[0][2], dL_dT11 * J[1][1] + dL_dT12 * J[1][2], 0.0f,
	// 	0.0f, 0.0f, 0.0f
	// );
	// glm::mat3 dL_dW = glm::mat3(
	// 	dL_dT00 * J[0][0] + dL_dT01 * J[1][0], dL_dT00 * J[0][1] + dL_dT01 * J[1][1], 0.0f,
	// 	dL_dT10 * J[0][0] + dL_dT11 * J[1][0], dL_dT10 * J[0][1] + dL_dT11 * J[1][1], 0.0f,
	// 	0.0f, 0.0f, 0.0f  
	// );
	// dL_dRs[idx] = dL_dW;

	float tz = 1.f / t.z;
	float tz2 = tz * tz;
	float tz3 = tz2 * tz;

	// Gradients of loss w.r.t. transformed Gaussian mean t
	float dL_dtx = x_grad_mul * -h_x * tz2 * dL_dJ02;
	float dL_dty = y_grad_mul * -h_y * tz2 * dL_dJ12;
	float dL_dtz = -h_x * tz2 * dL_dJ00 - h_y * tz2 * dL_dJ11 + (2 * h_x * t.x) * tz3 * dL_dJ02 + (2 * h_y * t.y) * tz3 * dL_dJ12
		- dL_dinvdepth[idx] * tz2;

	// Account for transformation of mean to t
	// t = transformPoint4x3(mean, viewmatrix);
	float3 dL_dmean_loc = { dL_dtx, dL_dty, dL_dtz }; //transformVec4x3Transpose({ dL_dtx, dL_dty, dL_dtz }, viewmatrix); // rot { dL_dtx, dL_dty, dL_dtz }; //

	// Gradients of loss w.r.t. Gaussian means, but only the portion 
	// that is caused because the mean affects the covariance matrix.
	// Additional mean gradient is accumulated in BACKWARD::preprocess.
	dL_dmeans_loc[idx] = dL_dmean_loc;
}

// Backward pass for the conversion of scale and rotation to a 
// 3D covariance matrix for each Gaussian. 
__device__ void computeCov3D(int idx, const glm::vec3 scale, float mod, const glm::vec4 rot, const float* dL_dcov3Ds, glm::vec3* dL_dscales, glm::vec4* dL_drots) // quats mat3 x2
{
	// **** // quats
	// Recompute (intermediate) results for the 3D covariance computation.
	glm::vec4 q = rot;// / glm::length(rot);
	float r = q.x;
	float x = q.y;
	float y = q.z;
	float z = q.w;

	glm::mat3 R = glm::mat3(
		1.f - 2.f * (y * y + z * z), 2.f * (x * y - r * z), 2.f * (x * z + r * y),
		2.f * (x * y + r * z), 1.f - 2.f * (x * x + z * z), 2.f * (y * z - r * x),
		2.f * (x * z - r * y), 2.f * (y * z + r * x), 1.f - 2.f * (x * x + y * y)
	);
	// **** // quats
	// glm::mat3 R = rot;
	// **** // quats

	glm::mat3 S = glm::mat3(1.0f);

	glm::vec3 s = mod * scale;
	S[0][0] = s.x;
	S[1][1] = s.y;
	S[2][2] = s.z;

	glm::mat3 M = S * R;

	const float* dL_dcov3D = dL_dcov3Ds + 6 * idx;

	glm::vec3 dunc(dL_dcov3D[0], dL_dcov3D[3], dL_dcov3D[5]);
	glm::vec3 ounc = 0.5f * glm::vec3(dL_dcov3D[1], dL_dcov3D[2], dL_dcov3D[4]);

	// Convert per-element covariance loss gradients to matrix form
	glm::mat3 dL_dSigma = glm::mat3(
		dL_dcov3D[0], 0.5f * dL_dcov3D[1], 0.5f * dL_dcov3D[2],
		0.5f * dL_dcov3D[1], dL_dcov3D[3], 0.5f * dL_dcov3D[4],
		0.5f * dL_dcov3D[2], 0.5f * dL_dcov3D[4], dL_dcov3D[5]
	);

	// Compute loss gradient w.r.t. matrix M
	// dSigma_dM = 2 * M
	glm::mat3 dL_dM = 2.0f * M * dL_dSigma;

	glm::mat3 Rt = glm::transpose(R);
	glm::mat3 dL_dMt = glm::transpose(dL_dM);

	// Gradients of loss w.r.t. scale
	glm::vec3* dL_dscale = dL_dscales + idx;
	dL_dscale->x = glm::dot(Rt[0], dL_dMt[0]);
	dL_dscale->y = glm::dot(Rt[1], dL_dMt[1]);
	dL_dscale->z = glm::dot(Rt[2], dL_dMt[2]);

	dL_dMt[0] *= s.x;
	dL_dMt[1] *= s.y;
	dL_dMt[2] *= s.z;

	// **** // quats
	// Gradients of loss w.r.t. normalized quaternion
	glm::vec4 dL_dq;
	dL_dq.x = 2 * z * (dL_dMt[0][1] - dL_dMt[1][0]) + 2 * y * (dL_dMt[2][0] - dL_dMt[0][2]) + 2 * x * (dL_dMt[1][2] - dL_dMt[2][1]);
	dL_dq.y = 2 * y * (dL_dMt[1][0] + dL_dMt[0][1]) + 2 * z * (dL_dMt[2][0] + dL_dMt[0][2]) + 2 * r * (dL_dMt[1][2] - dL_dMt[2][1]) - 4 * x * (dL_dMt[2][2] + dL_dMt[1][1]);
	dL_dq.z = 2 * x * (dL_dMt[1][0] + dL_dMt[0][1]) + 2 * r * (dL_dMt[2][0] - dL_dMt[0][2]) + 2 * z * (dL_dMt[1][2] + dL_dMt[2][1]) - 4 * y * (dL_dMt[2][2] + dL_dMt[0][0]);
	dL_dq.w = 2 * r * (dL_dMt[0][1] - dL_dMt[1][0]) + 2 * x * (dL_dMt[2][0] + dL_dMt[0][2]) + 2 * y * (dL_dMt[1][2] + dL_dMt[2][1]) - 4 * z * (dL_dMt[1][1] + dL_dMt[0][0]);

	// Gradients of loss w.r.t. unnormalized quaternion
	float4* dL_drot = (float4*)(dL_drots + idx);
	*dL_drot = float4{ dL_dq.x, dL_dq.y, dL_dq.z, dL_dq.w };//dnormvdv(float4{ rot.x, rot.y, rot.z, rot.w }, float4{ dL_dq.x, dL_dq.y, dL_dq.z, dL_dq.w });
	// **** // quats
	// glm::mat3* dL_drot = (dL_drots + idx);
	// *dL_drot = dL_dM;
	// **** // quats
}

// Backward pass of the preprocessing steps, except
// for the covariance computation and inversion
// (those are handled by a previous kernel call)
template<int C>
__global__ void preprocessCUDA(
	int P, int D, int M,
	const float3* means,
	const int* radii,
	const float* dc,
	const float* shs,
	const bool* clamped,
	const glm::vec3* scales,
	const glm::vec4* rotations, // quats mat3
	const float scale_modifier,
	const float* proj,
	const float* viewmatrix,
	const glm::vec3* campos,
	const float3* dL_dmean2D,
	glm::vec3* dL_dmeans,
	glm::mat3* dL_dRs,
	glm::vec3* dL_dt,
	float* dL_dcolor,
	float* dL_dcov3D,
	float* dL_ddc,
	float* dL_dsh,
	glm::vec3* dL_dscale,
	glm::vec4* dL_drot,  // quats mat3
	float* dL_dopacity)
{
	auto idx = cg::this_grid().thread_rank();
	if (idx >= P || !(radii[idx] > 0))
		return;

	float3 m_orig = means[idx];

	float3 m = transformPoint4x3({ m_orig.x, m_orig.y, m_orig.z }, viewmatrix); // rot

	// Taking care of gradients from the screenspace points
	float4 m_hom = transformPoint4x4(m, proj);
	float m_w = 1.0f / (m_hom.w + 0.0000001f);

	// Compute loss gradient w.r.t. 3D means due to gradients of 2D means
	// from rendering procedure
	glm::vec3 dL_dmean_loc;
	float mul1 = (proj[0] * m.x + proj[4] * m.y + proj[8] * m.z + proj[12]) * m_w * m_w;
	float mul2 = (proj[1] * m.x + proj[5] * m.y + proj[9] * m.z + proj[13]) * m_w * m_w;
	dL_dmean_loc.x = (proj[0] * m_w - proj[3] * mul1) * dL_dmean2D[idx].x + (proj[1] * m_w - proj[3] * mul2) * dL_dmean2D[idx].y;
	dL_dmean_loc.y = (proj[4] * m_w - proj[7] * mul1) * dL_dmean2D[idx].x + (proj[5] * m_w - proj[7] * mul2) * dL_dmean2D[idx].y;
	dL_dmean_loc.z = (proj[8] * m_w - proj[11] * mul1) * dL_dmean2D[idx].x + (proj[9] * m_w - proj[11] * mul2) * dL_dmean2D[idx].y;


	// That's the second part of the mean gradient. Previous computation
	// of cov2D and following SH conversion also affects it.
	dL_dt[idx] += dL_dmean_loc;
	// dL_dmeans[idx] += dL_dmean;

	dL_dmean_loc = dL_dt[idx];
	float3 dL_dmeanf3 = transformVec4x3Transpose({dL_dmean_loc.x, dL_dmean_loc.y, dL_dmean_loc.z}, viewmatrix);
	glm::vec3 dL_dmean = { dL_dmeanf3.x, dL_dmeanf3.y, dL_dmeanf3.z };
	dL_dmeans[idx] = dL_dmean;

	glm::mat3 dL_dR(
		dL_dmean_loc.x * m_orig.x, dL_dmean_loc.y * m_orig.x, dL_dmean_loc.z * m_orig.x,
		dL_dmean_loc.x * m_orig.y, dL_dmean_loc.y * m_orig.y, dL_dmean_loc.z * m_orig.y,
		dL_dmean_loc.x * m_orig.z, dL_dmean_loc.y * m_orig.z, dL_dmean_loc.z * m_orig.z
	);
	dL_dRs[idx] += dL_dR;

	// Compute gradient updates due to computing colors from SHs or SGs
	if (shs)
	{
		if (D < 0)
		{
			// Local SH gradient buffer (48 floats)
			float dL_dsh_local[48];
			// Initialize local gradients to 0 as computeColorFromSH accumulates
			for (int i=0; i<48; ++i) dL_dsh_local[i] = 0.f;

			// 1. Backprop from Color to SH coeffs
			// We must RECOMPUTE forward SH coeffs here locally first!
			
			float sh_forward_local[48];
			computeSHFromSG(idx, M, shs, sh_forward_local);

			// Extract DC (Band 0) from the first 3 floats (Interleaved R, G, B)
			glm::vec3 val_dc = {sh_forward_local[0], sh_forward_local[1], sh_forward_local[2]};
			glm::vec3* dc_ptr_local = &val_dc;
			glm::vec3* dc_ptr_adjusted = dc_ptr_local - idx;

			// Now call computeColorFromSH using LOCAL forward SHs and LOCAL gradient buffer
			// computeColorFromSH adds `idx * max_coeffs` to the pointer.
			// We must subtract it to access local stack correctly.
			
			int max_coeffs_sh = 16;
			
			// SH Pointer (Band 1 starts at offset 3 floats)
			glm::vec3* sh_ptr_local = (glm::vec3*)(sh_forward_local + 3);
			glm::vec3* sh_ptr_adjusted = sh_ptr_local - idx * max_coeffs_sh;
			
			// Gradient Pointer (Band 1 starts at offset 3 floats)
			glm::vec3* dL_dsh_ptr_local = (glm::vec3*)(dL_dsh_local + 3);
			glm::vec3* dL_dsh_ptr_adjusted = dL_dsh_ptr_local - idx * max_coeffs_sh;

			// DC Gradient (Local variable)
			glm::vec3 dL_ddc_val = {0.f, 0.f, 0.f};
			glm::vec3* dL_ddc_ptr_local = &dL_ddc_val;
			glm::vec3* dL_ddc_ptr_adjusted = dL_ddc_ptr_local - idx;

			// Use Degree 3 hardcoded for SG mode
			computeColorFromSH(idx, 3, max_coeffs_sh, (glm::vec3*)means, *campos, (float*)dc_ptr_adjusted, (float*)sh_ptr_adjusted, clamped, (glm::vec3*)dL_dcolor, (glm::vec3*)dL_dmeans, (glm::vec3*)dL_ddc_ptr_adjusted, (glm::vec3*)dL_dsh_ptr_adjusted);

			// Put DC gradient back into dL_dsh_local (Interleaved Band 0)
			dL_dsh_local[0] = dL_ddc_val.x;
			dL_dsh_local[1] = dL_ddc_val.y;
			dL_dsh_local[2] = dL_ddc_val.z;

			// 2. Backprop from SH coeffs to SG params
			// dL_dsh_local now contains gradients w.r.t SH coefficients (Interleaved).
			// We need to propagate this to dL_dsh (which points to global SG gradients).
			computeSGIGrad(idx, M, shs, dL_dsh_local, dL_dsh);
		}
		else
		{
			computeColorFromSH(idx, D, M, (glm::vec3*)means, *campos, dc, shs, clamped, (glm::vec3*)dL_dcolor, (glm::vec3*)dL_dmeans, (glm::vec3*)dL_ddc, (glm::vec3*)dL_dsh);
		}
	}

	// loss gradient w.r.t. camera translation
	// dL_dt[idx] = transformVec4x3(dL_dmeanf3, viewmatrix);
	// dL_dt[idx] = dL_dmean_loc;

	// loss gradient w.r.t. camera rotation matrix
	// dL_dRs[idx] = dL_dM;

	// Compute gradient updates due to computing covariance from scale/rotation
	if (scales)
		computeCov3D(idx, scales[idx], scale_modifier, rotations[idx], dL_dcov3D, dL_dscale, dL_drot);
}

template<uint32_t C>
__global__ void
PerGaussianRenderCUDA(
	const uint2* __restrict__ ranges,
	const uint32_t* __restrict__ point_list,
	int W, int H, int B,
	const uint32_t* __restrict__ per_tile_bucket_offset,
	const uint32_t* __restrict__ bucket_to_tile,
	const float* __restrict__ sampled_T, const float* __restrict__ sampled_ar, const float* __restrict__ sampled_ard,
	const float* __restrict__ bg_color,
	const float2* __restrict__ points_xy_image,
	const float4* __restrict__ conic_opacity,
	const float* __restrict__ colors,
	const float* __restrict__ depths,
	const float* __restrict__ final_Ts,
	const uint32_t* __restrict__ n_contrib,
	const uint32_t* __restrict__ max_contrib,
	const float* __restrict__ pixel_colors,
	const float* __restrict__ pixel_invDepths,
	const float* __restrict__ dL_dpixels,
	const float* __restrict__ dL_invdepths,
	float3* __restrict__ dL_dmean2D,
	float4* __restrict__ dL_dconic2D,
	float* __restrict__ dL_dopacity,
	float* __restrict__ dL_dcolors,
	float* __restrict__ dL_dinvdepths
) {
	// global_bucket_idx = warp_idx
	auto block = cg::this_thread_block();
	auto my_warp = cg::tiled_partition<32>(block);
	uint32_t global_bucket_idx = block.group_index().x * my_warp.meta_group_size() + my_warp.meta_group_rank();
	bool valid_bucket = global_bucket_idx < (uint32_t) B;
	if (!valid_bucket) return;

	bool valid_splat = false;

	uint32_t tile_id, bbm;
	uint2 range;
	int num_splats_in_tile, bucket_idx_in_tile;
	int splat_idx_in_tile, splat_idx_global;

	tile_id = bucket_to_tile[global_bucket_idx];
	range = ranges[tile_id];
	num_splats_in_tile = range.y - range.x;
	// What is the number of buckets before me? what is my offset?
	bbm = tile_id == 0 ? 0 : per_tile_bucket_offset[tile_id - 1];
	bucket_idx_in_tile = global_bucket_idx - bbm;
	splat_idx_in_tile = bucket_idx_in_tile * 32 + my_warp.thread_rank();
	splat_idx_global = range.x + splat_idx_in_tile;
	valid_splat = (splat_idx_in_tile < num_splats_in_tile);

	// if first gaussian in bucket is useless, then others are also useless
	if (bucket_idx_in_tile * 32 >= max_contrib[tile_id]) {
		return;
	}

	// Load Gaussian properties into registers
	int gaussian_idx = 0;
	float2 xy = {0.0f, 0.0f};
	float4 con_o = {0.0f, 0.0f, 0.0f, 0.0f};
	float c[C] = {0.0f};
	float invd = 0.f;
	if (valid_splat) {
		gaussian_idx = point_list[splat_idx_global];
		xy = points_xy_image[gaussian_idx];
		con_o = conic_opacity[gaussian_idx];
		for (int ch = 0; ch < C; ++ch)
			c[ch] = colors[gaussian_idx * C + ch];
		invd = 1.f / depths[gaussian_idx];
	}

	// Gradient accumulation variables
	float Register_dL_dmean2D_x = 0.0f;
	float Register_dL_dmean2D_y = 0.0f;
	float Register_dL_dconic2D_x = 0.0f;
	float Register_dL_dconic2D_y = 0.0f;
	float Register_dL_dconic2D_w = 0.0f;
	float Register_dL_dopacity = 0.0f;
	float Register_dL_dcolors[C] = {0.0f};
	float Register_dL_dinvdepths = 0.0f;
	
	// tile metadata
	const uint32_t horizontal_blocks = (W + BLOCK_X - 1) / BLOCK_X;
	const uint2 tile = {tile_id % horizontal_blocks, tile_id / horizontal_blocks};
	const uint2 pix_min = {tile.x * BLOCK_X, tile.y * BLOCK_Y};

	// values useful for gradient calculation
	float T;
	float T_final;
	float last_contributor;
	float ar[C];
	float ard;
	float dL_dpixel[C];
	float dL_invdepth;
	const float ddelx_dx = 0.5 * W;
	const float ddely_dy = 0.5 * H;

	// iterate over all pixels in the tile
	for (int i = 0; i < BLOCK_SIZE + 31; ++i) {
		// SHUFFLING

		// At this point, T already has my (1 - alpha) multiplied.
		// So pass this ready-made T value to next thread.
		T = my_warp.shfl_up(T, 1);
		last_contributor = my_warp.shfl_up(last_contributor, 1);
		T_final = my_warp.shfl_up(T_final, 1);
		for (int ch = 0; ch < C; ++ch) {
			ar[ch] = my_warp.shfl_up(ar[ch], 1);
			dL_dpixel[ch] = my_warp.shfl_up(dL_dpixel[ch], 1);
		}
		ard = my_warp.shfl_up(ard, 1);
		dL_invdepth = my_warp.shfl_up(dL_invdepth, 1);

		// which pixel index should this thread deal with?
		int idx = i - my_warp.thread_rank();
		const uint2 pix = {pix_min.x + idx % BLOCK_X, pix_min.y + idx / BLOCK_X};
		const uint32_t pix_id = W * pix.y + pix.x;
		const float2 pixf = {(float) pix.x, (float) pix.y};
		bool valid_pixel = pix.x < W && pix.y < H;
		
		// every 32nd thread should read the stored state from memory
		// TODO: perhaps store these things in shared memory?
		if (valid_splat && valid_pixel && my_warp.thread_rank() == 0 && idx < BLOCK_SIZE) {
			T = sampled_T[global_bucket_idx * BLOCK_SIZE + idx];
			for (int ch = 0; ch < C; ++ch)
				ar[ch] = -pixel_colors[ch * H * W + pix_id] + sampled_ar[global_bucket_idx * BLOCK_SIZE * C + ch * BLOCK_SIZE + idx];
			ard = -pixel_invDepths[pix_id] + sampled_ard[global_bucket_idx * BLOCK_SIZE + idx];
			T_final = final_Ts[pix_id];
			last_contributor = n_contrib[pix_id];
			for (int ch = 0; ch < C; ++ch) {
				dL_dpixel[ch] = dL_dpixels[ch * H * W + pix_id];
			}
			dL_invdepth = dL_invdepths[pix_id];
		}

		// do work
		if (valid_splat && valid_pixel && 0 <= idx && idx < BLOCK_SIZE) {
			if (W <= pix.x || H <= pix.y) continue;

			if (splat_idx_in_tile >= last_contributor) continue;

			// compute blending values
			const float2 d = { xy.x - pixf.x, xy.y - pixf.y };
			const float power = -0.5f * (con_o.x * d.x * d.x + con_o.z * d.y * d.y) - con_o.y * d.x * d.y;
			if (power > 0.0f) continue;
			const float G = exp(power);
			const float alpha = min(0.99f, con_o.w * G);
			if (alpha < 1.0f / 255.0f) continue;
			const float weight = alpha * T;

			// add the gradient contribution of this pixel's colour to the gaussian
			float bg_dot_dpixel = 0.0f;
			float dL_dalpha = 0.0f;
			for (int ch = 0; ch < C; ++ch) {
				ar[ch] += weight * c[ch]; // TODO: check
				const float &dL_dchannel = dL_dpixel[ch];
				Register_dL_dcolors[ch] += weight * dL_dchannel;
				dL_dalpha += ((c[ch] * T) - (1.0f / (1.0f - alpha)) * (-ar[ch])) * dL_dchannel;

				bg_dot_dpixel += bg_color[ch] * dL_dpixel[ch];
			}

			// // add the gradient contribution of this pixel's depth to the gaussian
			ard += weight * invd;
			Register_dL_dinvdepths += weight * dL_invdepth;
			dL_dalpha += ((invd * T) - (1.0f / (1.0f - alpha)) * (-ard)) * dL_invdepth;

			// Account for last sample for colour
			dL_dalpha += (-T_final / (1.0f - alpha)) * bg_dot_dpixel;
			T *= (1.0f - alpha);

			// Helpful reusable temporary variables
			const float dL_dG = con_o.w * dL_dalpha;
			const float gdx = G * d.x;
			const float gdy = G * d.y;
			const float dG_ddelx = -gdx * con_o.x - gdy * con_o.y;
			const float dG_ddely = -gdy * con_o.z - gdx * con_o.y;

			// accumulate the gradients
			const float tmp_x = dL_dG * dG_ddelx * ddelx_dx;
			Register_dL_dmean2D_x += tmp_x;
			const float tmp_y = dL_dG * dG_ddely * ddely_dy;
			Register_dL_dmean2D_y += tmp_y;

			Register_dL_dconic2D_x += -0.5f * gdx * d.x * dL_dG;
			Register_dL_dconic2D_y += -0.5f * gdx * d.y * dL_dG;
			Register_dL_dconic2D_w += -0.5f * gdy * d.y * dL_dG;
			Register_dL_dopacity += G * dL_dalpha;
		}
	}

	// finally add the gradients using atomics
	if (valid_splat) {
		atomicAdd(&dL_dmean2D[gaussian_idx].x, Register_dL_dmean2D_x);
		atomicAdd(&dL_dmean2D[gaussian_idx].y, Register_dL_dmean2D_y);
		atomicAdd(&dL_dconic2D[gaussian_idx].x, Register_dL_dconic2D_x);
		atomicAdd(&dL_dconic2D[gaussian_idx].y, Register_dL_dconic2D_y);
		atomicAdd(&dL_dconic2D[gaussian_idx].w, Register_dL_dconic2D_w);
		atomicAdd(&dL_dopacity[gaussian_idx], Register_dL_dopacity);
		for (int ch = 0; ch < C; ++ch) {
			atomicAdd(&dL_dcolors[gaussian_idx * C + ch], Register_dL_dcolors[ch]);
		}
		atomicAdd(&dL_dinvdepths[gaussian_idx], Register_dL_dinvdepths);
	}
}

void BACKWARD::preprocess(
	int P, int D, int M,
	const float3* means3D,
	const int* radii,
	const float* dc,
	const float* shs,
	const bool* clamped,
	const float* opacities,
	const glm::vec3* scales,
	const glm::vec4* rotations, 
	const float scale_modifier,
	const float* cov3Ds,
	const float* viewmatrix,
	const float* projmatrix,
	const float focal_x, float focal_y,
	const float tan_fovx, float tan_fovy,
	const glm::vec3* campos,
	const float3* dL_dmean2D,
	const float* dL_dconic,
	const float* dL_dinvdepth,
	float* dL_dopacity,
	glm::vec3* dL_dmean3D,
	float* dL_dcolor,
	float* dL_dcov3D,
	float* dL_ddc,
	float* dL_dsh,
	glm::vec3* dL_dscale,
	glm::vec4* dL_drot,
	glm::mat3* dL_dRs,
	glm::vec3* dL_dt)
{
	// Propagate gradients for the path of 2D conic matrix computation. 
	// Somewhat long, thus it is its own kernel rather than being part of 
	// "preprocess". When done, loss gradient w.r.t. 3D means has been
	// modified and gradient w.r.t. 3D covariance matrix has been computed.	
	computeCov2DCUDA << <(P + 255) / 256, 256 >> > (
		P,
		means3D,
		radii,
		cov3Ds,
		focal_x,
		focal_y,
		tan_fovx,
		tan_fovy,
		viewmatrix,
		opacities,
		dL_dconic,
		dL_dopacity,
		dL_dinvdepth,
		(float3*)dL_dt,
		dL_dcov3D, 
		dL_dRs);

	// Propagate gradients for remaining steps: finish 3D mean gradients,
	// propagate color gradients to SH (if desireD), propagate 3D covariance
	// matrix gradients to scale and rotation.
	preprocessCUDA<NUM_CHANNELS_3DGS> << < (P + 255) / 256, 256 >> > (
		P, D, M,
		(float3*)means3D,
		radii,
		dc,
		shs,
		clamped,
		(glm::vec3*)scales,
		(glm::vec4*)rotations, // quats mat3
		scale_modifier,
		projmatrix,
		viewmatrix,
		campos,
		(float3*)dL_dmean2D,
		(glm::vec3*)dL_dmean3D,
		(glm::mat3*) dL_dRs,
		(glm::vec3*) dL_dt,
		dL_dcolor,
		dL_dcov3D,
		dL_ddc,
		dL_dsh,
		dL_dscale,
		dL_drot,
		dL_dopacity);
}

void BACKWARD::render(
	const dim3 grid, dim3 block,
	const uint2* ranges,
	const uint32_t* point_list,
	int W, int H, int R, int B,
	const uint32_t* per_bucket_tile_offset,
	const uint32_t* bucket_to_tile,
	const float* sampled_T, const float* sampled_ar, const float* sampled_ard,
	const float* bg_color,
	const float2* means2D,
	const float4* conic_opacity,
	const float* colors,
	const float* depths,
	const float* final_Ts,
	const uint32_t* n_contrib,
	const uint32_t* max_contrib,
	const float* pixel_colors,
	const float* pixel_invDepths,
	const float* dL_dpixels,
	const float* dL_invdepths,
	float3* dL_dmean2D,
	float4* dL_dconic2D,
	float* dL_dopacity,
	float* dL_dcolors,
	float* dL_dinvdepths)
{
	const int THREADS = 32;
	PerGaussianRenderCUDA<NUM_CHANNELS_3DGS> <<<((B*32) + THREADS - 1) / THREADS,THREADS>>>(
		ranges,
		point_list,
		W, H, B,
		per_bucket_tile_offset,
		bucket_to_tile,
		sampled_T, sampled_ar, sampled_ard,
		bg_color,
		means2D,
		conic_opacity,
		colors,
		depths,
		final_Ts,
		n_contrib,
		max_contrib,
		pixel_colors,
		pixel_invDepths,
		dL_dpixels,
		dL_invdepths,
		dL_dmean2D,
		dL_dconic2D,
		dL_dopacity,
		dL_dcolors, 
		dL_dinvdepths
		);
}
